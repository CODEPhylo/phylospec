moves = VectorMoves()
monitors = VectorMonitors()

observedAlignment := readDiscreteCharacterData( file="hallo.nex" )
kappa ~ dnLognormal( mean=1.0, sd=0.5 )
moves.append( mvScaleBactrian( kappa ) )
baseFrequencies ~ dnDirichlet( alpha=v( 2.0, 2.0, 2.0, 2.0 ) )
moves.append( mvDirichletSimplex( baseFrequencies ) )
Q := fnHKY( kappa=kappa, baseFrequencies=baseFrequencies )
populationSize ~ dnLognormal( mean=3.0, sd=2.0 )
moves.append( mvScaleBactrian( populationSize ) )
tree ~ dnCoalescent( theta=populationSize, taxa=observedAlignment.taxa() )
moves.append( mvNarrow( tree ) )
moves.append( mvNNI( tree ) )
moves.append( mvFNPR( tree ) )
moves.append( mvSubtreeScale( tree ) )
moves.append( mvNodeTimeSlideUniform( tree ) )
moves.append( mvRootTimeScaleBactrian( tree ) )
moves.append( mvTreeScale( tree ) )
alignment ~ dnPhyloCTMC( tree=tree, Q=Q )
alignment.clamp( observedAlignment )

monitors.append( mnModel( filename = "model.log", printgen = 10 ) )
monitors.append( mnNexus( filename = "model.trees", tree = tree, printgen = 10 ) )
monitors.append( mnScreen( printgen = 10 ) )

mymodel = model( kappa, baseFrequencies, populationSize, tree, alignment )
mymcmc = mcmc( mymodel, monitors, moves )
mymcmc.run( generations=20000, tuningInterval=200 )

q()